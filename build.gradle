plugins {
    id 'java'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'com.sb.jini' version '0.5.0'
}

ext {
    outDir = project.layout.buildDirectory.dir("macApp").get().asFile
    appDir = "$outDir/Transmission Remote.app" as File
}

group = 'com.sergeybochkov'
version = '3.0.7'

repositories() {
    mavenCentral()
    maven {
        url = "https://maven.sergeybochkov.com/releases"
    }
}

dependencies {
    implementation 'com.sb:bdev-rt:1.0.7.RELEASE'
    implementation 'cordelia:cordelia:6.0.1.RELEASE'

    implementation 'com.formdev:flatlaf:3.7'
    implementation 'com.formdev:flatlaf-extras:3.7'
    implementation 'com.formdev:flatlaf-fonts-inter:4.1'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.32'
    implementation 'com.miglayout:miglayout-swing:11.4.3'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    testImplementation(platform('org.junit:junit-bom:6.0.3'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    testImplementation 'org.assertj:assertj-core:3.27.7'
}

application {
    mainClass = "com.sb.transmissionremote.TransmissionRemote"
    if (System.getProperty("os.name").contains("Mac OS")) {
        applicationDefaultJvmArgs = [
                '-Xdock:name=Transmission Remote',
                '--enable-native-access=ALL-UNNAMED'
        ]
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('macApp') {
    dependsOn 'build'
    doLast {
        delete outDir
        file(appDir).mkdirs()
        copy {
            from tarTree(distTar.archiveFile)
            into "$appDir/Contents/MacOS"
        }
        copy {
            from 'src/main/resources/style/transmission-remote.icns'
            into "$appDir/Contents/Resources"
        }
        copy {
            from 'src/main/resources/Info.plist'
            into "$appDir/Contents"
            filter { line -> line.replace('{{ root }}', distTar.getArchiveBaseName().get() + "-" + distTar.getArchiveVersion().get()) }
        }
        copy {
            from 'src/main/resources/PkgInfo'
            into "$appDir/Contents"
        }
    }
}

tasks.named('jini').configure {
    mainClassName = 'com.sb.transmissionremote.TransmissionRemote'
    exeFilename = 'transmission-remote.exe'
    vmArgs = ['-Xmx64m', '--enable-native-access=ALL-UNNAMED']
    icon = "${projectDir}/src/main/resources/style/transmission-remote.ico"
    fileVersion = "1.0.0." + project.version.replace('.', '')
    productVersion = "1.0.0." + project.version.replace('.', '')
    comments = "Transmission Remote GUI"
    fileDescription = "Transmission Remote GUI"
    productName = "Transmission Remote GUI"
    copyright = "Bochkov Sergey"
}

tasks.named('dependencyUpdates').configure {
    rejectVersionIf {
        return  ['.*-alpha\\d+', '.*-m\\d+$', '.*-rc\\d+'].any { part ->
            it.candidate.version.toLowerCase().matches(part)
        }
    }
}
