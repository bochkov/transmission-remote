plugins {
    id 'java'
    id 'application'
    id 'edu.sc.seis.launch4j' version '3.0.6'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

ext {
    outDir = project.layout.buildDirectory.dir("macApp").get().asFile
    appDir = "$outDir/Transmission Remote.app" as File
}

group = 'com.sergeybochkov'
version = '3.0.4'


repositories() {
    mavenCentral()
    maven {
        url = "https://maven.sergeybochkov.com/releases"
    }
}

dependencies {
    implementation 'com.sb:bdev-rt:1.0.7.RELEASE'
    implementation 'cordelia:cordelia:5.4.0.RELEASE'
    implementation 'com.formdev:flatlaf:3.6'
    implementation 'com.formdev:flatlaf-extras:3.6'
    implementation 'com.formdev:flatlaf-fonts-inter:4.1'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation 'com.miglayout:miglayout-swing:11.4.2'

    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = "com.sb.transmissionremote.TransmissionRemote"
    if (System.getProperty("os.name").contains("Mac OS")) {
        applicationDefaultJvmArgs = [
                '-Xdock:name=Transmission Remote'
        ]
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('macApp') {
    dependsOn 'build'
    doLast {
        delete outDir
        file(appDir).mkdirs()
        copy {
            from tarTree(distTar.archiveFile)
            into "$appDir/Contents/MacOS"
        }
        copy {
            from 'src/main/resources/style/transmission-remote.icns'
            into "$appDir/Contents/Resources"
        }
        copy {
            from 'src/main/resources/Info.plist'
            into "$appDir/Contents"
            filter { line -> line.replace('{{ root }}', distTar.getArchiveBaseName().get() + "-" + distTar.getArchiveVersion().get()) }
        }
        copy {
            from 'src/main/resources/PkgInfo'
            into "$appDir/Contents"
        }
    }
}

launch4j {
    mainClassName = 'com.sb.transmissionremote.TransmissionRemote'
    icon = "${projectDir}/src/main/resources/style/transmission-remote.ico"
}

tasks.named('dependencyUpdates').configure {
    rejectVersionIf {
        return  ['.*-alpha\\d+', '.*-m\\d+$', '.*-rc\\d+'].any { part ->
            it.candidate.version.toLowerCase().matches(part)
        }
    }
}