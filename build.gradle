plugins {
    id 'java'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.47.0'
}

group = 'com.sergeybochkov'
version = '3.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories() {
    mavenCentral()
    maven {
        url "https://maven.sergeybochkov.com/releases"
    }
}

dependencies {
    implementation 'com.sb:bdev-rt:1.0.4.RELEASE'
    implementation 'cordelia:cordelia:5.4.0.RELEASE'
    implementation 'com.formdev:flatlaf:3.2'
    implementation 'com.formdev:flatlaf-extras:3.2'
    implementation 'ch.qos.logback:logback-classic:1.4.11'
    implementation 'com.miglayout:miglayout-swing:11.1'

    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

application {
    mainClass = "com.sb.transmissionremote.TransmissionRemote"
    applicationDefaultJvmArgs = [
            '-Xdock:name=Transmission Remote'
    ]
}

task("macApp", dependsOn: 'build') {
    doLast {
        def outDir = "$buildDir/macApp" as File
        def appDir = "$outDir/Transmission Remote.app" as File
        delete outDir
        project.file(appDir).mkdirs()
        copy {
            from tarTree(distTar.archiveFile)
            into "$appDir/Contents/MacOS"
        }
        copy {
            from 'src/main/resources/style/transmission-remote.icns'
            into "$appDir/Contents/Resources"
        }
        copy {
            from 'src/main/resources/Info.plist'
            into "$appDir/Contents"
            filter { line -> line.replace('{{ root }}', distTar.getArchiveBaseName().get() + "-" + distTar.getArchiveVersion().get()) }
        }
        copy {
            from 'src/main/resources/PkgInfo'
            into "$appDir/Contents"
        }
    }
}