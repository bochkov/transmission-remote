plugins {
    id "application"
}

group = 'com.sergeybochkov'
version = '3.0'

apply plugin: "java"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories() {
    mavenCentral()
    maven {
        url "https://nexus.sergeybochkov.com/repository/maven-public"
    }
}

mainClassName = "com.sb.transmissionremote.TransmissionRemote"

dependencies {
    implementation 'cordelia:cordelia:2.0.RELEASE'
    implementation 'com.formdev:flatlaf:1.2'
    implementation 'com.formdev:flatlaf-extras:1.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.miglayout:miglayout-core:11.0'
    implementation 'com.miglayout:miglayout-swing:11.0'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    
    testImplementation 'junit:junit:4.12'
}

task("macApp", dependsOn: 'build') {
    doLast {
        def outDir = "$buildDir/macApp" as File
        def appDir = "$outDir/Transmission Remote.app" as File
        delete outDir
        project.file(appDir).mkdirs()
        copy {
            from tarTree(distTar.archiveFile)
            into "$appDir/Contents/MacOS"
        }
        copy {
            from 'src/main/resources/style/transmission-remote.icns'
            into "$appDir/Contents/Resources"
        }
        copy {
            from 'src/main/resources/Info.plist'
            into "$appDir/Contents"
            filter { line -> line.replace('{{ root }}', distTar.getArchiveBaseName().get() + "-" + distTar.getArchiveVersion().get()) }
        }
        copy {
            from 'src/main/resources/PkgInfo'
            into "$appDir/Contents"
        }
    }
}